---
title: Terraform Provider
description: Configuration as code with the Teleport Terraform Provider
videoBanner: YgNHD4SS8dg
version: '14.x'
---

This guide will explain how to:

- Set up Teleport's Terraform provider on Linux and Mac.
- Configure Teleport users and roles using the Terraform provider.

## Prerequisites

<Tabs>
  <Tab title="Teleport Team">
    - A Teleport Team account. If you do not have one, visit the [signup
      page](https://goteleport.com/signup/) to begin your free trial.

    - The `tctl` admin tool and `tsh` client tool version >= 14.0.0-dev.

      ```code
      $ tctl version
      # Teleport v14.0.0-dev go1.20

      $ tsh version
      # Teleport v14.0.0-dev go1.20
      ```

      See [Installation](/ver/14.x/installation) for details.
  </Tab>

  <Tab title="Teleport Community Edition">
    - A running Teleport cluster. For details on how to set this up, see our
      [Getting Started](/ver/14.x/index) guide.

    - The `tctl` admin tool and `tsh` client tool version >= 14.0.0-dev.

      ```code
      $ tctl version
      # Teleport v14.0.0-dev go1.20

      $ tsh version
      # Teleport v14.0.0-dev go1.20
      ```

      See [Installation](/ver/14.x/installation) for details.
  </Tab>

  <Tab title="Teleport Enterprise">
    - A running Teleport Enterprise cluster. For details on how to set this up, see our Enterprise
      [Getting Started](/ver/14.x/choose-an-edition/teleport-enterprise/introduction) guide.

    - The Enterprise `tctl` admin tool and `tsh` client tool version >= 14.0.0-dev,
      which you can download by visiting your [Teleport account](https://teleport.sh).

      ```code
      $ tctl version
      # Teleport Enterprise v14.0.0-dev go1.20

      $ tsh version
      # Teleport v14.0.0-dev go1.20
      ```
  </Tab>

  <Tab title="Teleport Enterprise Cloud">
    - A Teleport Enterprise Cloud account. If you do not have one, visit the [signup
      page](https://goteleport.com/signup/) to begin a free trial of Teleport Team
      and upgrade to Teleport Enterprise Cloud.

    - The Enterprise `tctl` admin tool and `tsh` client tool version >= 13.2.0.
      To download these tools, visit the [Downloads](/ver/14.x/choose-an-edition/teleport-cloud/downloads) page.

      ```code
      $ tctl version
      # Teleport Enterprise v13.2.0 go1.20

      $ tsh version
      # Teleport v13.2.0 go1.20
      ```
  </Tab>
</Tabs>

- [Terraform >= 1.0.0+](https://learn.hashicorp.com/tutorials/terraform/install-cli)

  ```code
  $ terraform version
  # Terraform v1.0.0
  ```

- Make sure you can connect to Teleport. Log in to your cluster using `tsh`, then use `tctl`
  remotely:
  {/* Ignoring scope linting since we use this partial throughout the docs and
  cannot guarantee that it will line up with a page's configured scopes*/}
  {/*lint ignore scopes*/}
  ```code
  $ tsh login --proxy=teleport.example.com --user=email@example.com
  $ tctl status
  # Cluster  teleport.example.com
  # Version  14.0.0-dev
  # CA pin   sha256:abdc1245efgh5678abdc1245efgh5678abdc1245efgh5678abdc1245efgh5678
  ```
  You can run subsequent `tctl` commands in this guide on your local machine.

  For full privileges, you can also run `tctl` commands on your Auth Service host.
  {/*lint ignore scopes*/}
  ```code
  $ tsh login --proxy=myinstance.teleport.sh --user=email@example.com
  $ tctl status
  # Cluster  myinstance.teleport.sh
  # Version  13.2.0
  # CA pin   sha256:sha-hash-here
  ```
  You must run subsequent `tctl` commands in this guide on your local machine.

Create a folder called `teleport-terraform` to hold some temporary files:

```code
$ mkdir -p teleport-terraform
$ cd teleport-terraform
```

## Step 1/3. Create Teleport credentials for Terraform

Terraform needs a signed identity file from your Teleport cluster's certificate
authority in order to manage resources in the cluster.

<Tip>
  If you intend to run Terraform from a CI/CD platform,
  [Machine ID](/ver/14.x/machine-id/introduction) is often a better option for
  generating credentials. Machine ID can provision short-lived certificates for
  CI/CD workflows as opposed to manually generating impersonated credentials with
  a long TTL.
</Tip>

### Create the Terraform user

Create a local Teleport user named `terraform`, and a matching role granting the
necessary permissions for Terraform to manage resources in your cluster.

Put the following content into `terraform.yaml`:

```yaml
kind: role
metadata:
  name: terraform
spec:
  allow:
    db_labels:
      '*': '*'
    app_labels:
      '*': '*'
    rules:
      - resources:
        - app
        - cluster_auth_preference
        - cluster_networking_config
        - db
        - device
        - github
        - login_rule
        - oidc
        - okta_import_rule
        - role
        - saml
        - session_recording_config
        - token
        - trusted_cluster
        - user
        verbs: ['list','create','read','update','delete']
version: v6
---
kind: user
metadata:
  name: terraform
spec:
  roles: ['terraform']
version: v2
```

Create the `terraform` user and role.

```code
$ tctl create terraform.yaml
```

### Enable impersonation

The `terraform` user cannot log in to get credentials, another user must
**impersonate** this account in order to request a certificate.

Create a role that enables your user to impersonate the Terraform user. First, paste
the following YAML document into a file called `terraform-impersonator.yaml`:

```yaml
kind: role
version: v6
metadata:
  name: terraform-impersonator
spec:
  allow:
    # This impersonate rule will allow any user with this role to impersonate
    # and generate certificates for the user named "terraform" with a role also
    # named "terraform".
    impersonate:
      users: ['terraform']
      roles: ['terraform']
```

Next, create the role:

```code
$ tctl create terraform-impersonator.yaml
```

Assign the `terraform-impersonator` role to your Teleport user by running the following
commands, depending on whether you authenticate as a local Teleport user or via
the `github`, `saml`, or `oidc` authentication connectors:

<Tabs>
  <Tab title="Local User">
    Retrieve your local user's configuration resource:

    ```code
    $ tctl get users/$(tsh status -f json | jq -r '.active.username') > out.yaml
    ```

    Edit `out.yaml`, adding `terraform-impersonator` to the list of existing roles:

    ```diff
      roles:
       - access
       - auditor
       - editor
    +  - terraform-impersonator
    ```

    Apply your changes:

    ```code
    $ tctl create -f out.yaml
    ```
  </Tab>

  <Tab title="GitHub">
    Retrieve your `github`  configuration resource:

    ```code
    $ tctl get github/github --with-secrets > github.yaml
    ```

    Edit `github.yaml`, adding `terraform-impersonator` to the
    `teams_to_roles` section. The team you will map to this role will depend on how
    you have designed your organization's RBAC, but it should be the smallest team
    possible within your organization. This team must also include your user.

    Here is an example:

    ```diff
      teams_to_roles:
        - organization: octocats
          team: admins
          roles:
            - access
    +       - terraform-impersonator
    ```

    Apply your changes:

    ```code
    $ tctl create -f github.yaml
    ```

    <Warning>
      Note the `--with-secrets` flag in the `tctl get` command. This adds the value of
      `spec.signing_key_pair.private_key` to `github.yaml`. This is a sensitive value,
      so take precautions when creating this file and remove it after updating the resource.
    </Warning>
  </Tab>

  <Tab title="SAML">
    Retrieve your `saml`  configuration resource:

    ```code
    $ tctl get --with-secrets saml/mysaml > saml.yaml
    ```

    Edit `saml.yaml`, adding `terraform-impersonator` to the
    `attributes_to_roles` section. The attribute you will map to this role will
    depend on how you have designed your organization's RBAC, but it should be the
    smallest group possible within your organization. This group must also include
    your user.

    Here is an example:

    ```diff
      attributes_to_roles:
        - name: "groups"
          value: "my-group"
          roles:
            - access
    +       - terraform-impersonator
    ```

    Apply your changes:

    ```code
    $ tctl create -f saml.yaml
    ```

    <Warning>
      Note the `--with-secrets` flag in the `tctl get` command. This adds the value of
      `spec.signing_key_pair.private_key` to `saml.yaml`. This is a sensitive value,
      so take precautions when creating this file and remove it after updating the resource.
    </Warning>
  </Tab>

  <Tab title="OIDC">
    Retrieve your `oidc`  configuration resource:

    ```code
    $ tctl get oidc/myoidc --with-secrets > oidc.yaml
    ```

    Edit `oidc.yaml`, adding `terraform-impersonator` to the
    `claims_to_roles` section. The claim you will map to this role will depend on
    how you have designed your organization's RBAC, but it should be the smallest
    group possible within your organization. This group must also include your
    user.

    Here is an example:

    ```diff
      claims_to_roles:
        - name: "groups"
          value: "my-group"
          roles:
            - access
    +       - terraform-impersonator
    ```

    Apply your changes:

    ```code
    $ tctl create -f oidc.yaml
    ```

    <Warning>
      Note the `--with-secrets` flag in the `tctl get` command. This adds the value of
      `spec.signing_key_pair.private_key` to `oidc.yaml`. This is a sensitive value,
      so take precautions when creating this file and remove it after updating the resource.
    </Warning>
  </Tab>
</Tabs>

Log out of your Teleport cluster and log in again to assume the new role.

Next, request a signed identity file for the Terraform user:

```code
$ tctl auth sign --user=terraform --out=terraform-identity
```

This command should result in one file: `terraform-identity`.

## Step 2/3. Create a Terraform configuration

Paste the following into a file called `main.tf` to define an example user and
role using Terraform.

<Tabs>
  <Tab title="Cloud-Hosted">
    ```
    terraform {
      required_providers {
        teleport = {
          source  = "terraform.releases.teleport.dev/gravitational/teleport"
          version = ">= 14.0.0-dev"
        }
      }
    }

    provider "teleport" {
      # Update addr to point to your Teleport Cloud tenant URL's host:port
      addr               = "mytenant.teleport.sh:443"
      identity_file_path = "terraform-identity"
    }

    resource "teleport_role" "terraform-test" {
      metadata = {
        name        = "terraform-test"
        description = "Terraform test role"
        labels = {
          example = "yes"
        }
      }

      spec = {
        options = {
          forward_agent           = false
          max_session_ttl         = "30m"
          port_forwarding         = false
          client_idle_timeout     = "1h"
          disconnect_expired_cert = true
          permit_x11_forwarding   = false
          request_access          = "denied"
        }

        allow = {
          logins = ["this-user-does-not-exist"]

          rules = [
            {
              resources = ["user", "role"]
              verbs     = ["list"]
            }
          ]

          request = {
            roles = ["example"]
            claims_to_roles = [
              {
                claim = "example"
                value = "example"
                roles = ["example"]
              }
            ]
          }

          node_labels = {
            key    = ["example"]
            alabel = ["with", "multiple", "values"]
          }
        }

        deny = {
          logins = ["anonymous"]
        }
      }
    }

    resource "teleport_user" "terraform-test" {
      metadata = {
        name        = "terraform-test"
        description = "Test terraform user"
        expires     = "2022-10-12T07:20:50Z"

        labels = {
          test = "true"
        }
      }

      spec = {
        roles = ["terraform-test"]
      }
    }

    ```
  </Tab>

  <Tab title="Self-Hosted">
    ```
    terraform {
      required_providers {
        teleport = {
          source  = "terraform.releases.teleport.dev/gravitational/teleport"
          version = ">= 14.0.0-dev"
        }
      }
    }

    provider "teleport" {
      # Update addr to point to Teleport Auth/Proxy
      # addr              = "auth.example.com:3025"
      addr               = "proxy.example.com:443"
      identity_file_path = "terraform-identity"
    }

    resource "teleport_role" "terraform-test" {
      metadata = {
        name        = "terraform-test"
        description = "Terraform test role"
        labels = {
          example = "yes"
        }
      }

      spec = {
        options = {
          forward_agent           = false
          max_session_ttl         = "30m"
          port_forwarding         = false
          client_idle_timeout     = "1h"
          disconnect_expired_cert = true
          permit_x11_forwarding   = false
          request_access          = "denied"
        }

        allow = {
          logins = ["this-user-does-not-exist"]

          rules = [
            {
              resources = ["user", "role"]
              verbs     = ["list"]
            }
          ]

          request = {
            roles = ["example"]
            claims_to_roles = [
              {
                claim = "example"
                value = "example"
                roles = ["example"]
              }
            ]
          }

          node_labels = {
            key    = ["example"]
            alabel = ["with", "multiple", "values"]
          }
        }

        deny = {
          logins = ["anonymous"]
        }
      }
    }

    resource "teleport_user" "terraform-test" {
      metadata = {
        name        = "terraform-test"
        description = "Test terraform user"
        expires     = "2022-10-12T07:20:50Z"

        labels = {
          test = "true"
        }
      }

      spec = {
        roles = ["terraform-test"]
      }
    }

    ```
  </Tab>
</Tabs>

## Step 3/3. Apply the configuration

Check the contents of the `teleport-terraform` folder:

```code
$ ls
# main.tf  terraform-identity  terraform-impersonator.yaml  terraform.yaml
```

Init terraform and apply the spec:

```code
$ terraform init
$ terraform apply
```

## Next Steps

- Find the full list of [supported Terraform provider resources](/ver/14.x/reference/terraform-provider).
- Read more about [impersonation](/ver/14.x/access-controls/guides/impersonation).
